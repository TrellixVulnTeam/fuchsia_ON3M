# Copyright 2020 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/toolchain/toolchain_environment.gni")
import("//build/toolchain/zircon/zircon_toolchain_suite.gni")

if (current_toolchain == default_toolchain) {
  # Define a special environment for building code that runs in physical
  # memory with the MMU disabled, at early boot.
  zircon_toolchain_suite("physmem_riscv64") {
    cpu = "riscv64"
    os = "fuchsia"
    environment = "physmem"
    with_shared = false
    configs = [ "//zircon/kernel/arch/riscv64:physmem_config" ]
    toolchain_tags = [
      "kernel",
      "strict-align",
    ]
    exclude_variant_tags = [ "instrumented" ]
  }
}

if (toolchain_environment == "physmem") {
  # This is the top config for the physmem environment.
  config("physmem_config") {
    configs = [
      "//zircon/kernel:headers",
      "//zircon/kernel:standalone",
      "//zircon/kernel:warnings",
      "//build/config/zircon:no_sanitizers",
    ]

    cflags = [
      "-fpie",
    ]

    include_dirs = [ "include" ]
  }

  group("physmem_config_deps") {
  }
}

if (toolchain_environment == "physmem" ||
    toolchain_environment == "kernel.phys" ||
    toolchain_environment == "kernel") {
  # These set the ABI contract between C++ and assembly code.
  # This is kept consistent between the kernel proper and phys
  # so (pure PIC) code should be callable from the kernel proper.
  config("abi") {
  }

  # This is used pervasively throughout the kernel on riscv64.
  config("kernel") {
    cflags = []

    cflags += [
      # Use shadow-call-stack rather than safe-stack for the kernel,
      # regardless of the compiler's default.
      "-fno-sanitize=safe-stack",
      "-fno-stack-protector",
    ]

    # Assembly code needs to use `#if __has_feature(...)` so make sure
    # it always sees all the same `-fsanitize=...` flags and the like.
    asmflags = cflags
    ldflags = cflags

    defines = [ "ARCH_RISCV64" ]

    # For #include <arch/foo.h>.
    include_dirs = [ "include" ]

    configs = [
      # <arch/current_thread.h> has #include <lib/arch/intrin.h>.
      "//zircon/kernel/lib/arch/riscv64:headers.config",
    ]

    # Align the kernel's segments to 64k so it can use "combined pages" to
    # reduce the TLB load.
    ldflags += [ "-Wl,-z,max-page-size=65536" ]
  }

  source_set("riscv64") {
    sources = [
      "arch.cc",
      "asid_allocator.cc",
      "asm.S",
      "boot-mmu.cc",
      "debugger.cc",
      "exceptions_c.cc",
      "mmu.cc",
      "mp.cc",
      "perf_mon.cc",
      "spinlock.cc",
      "start.S",
      "thread.cc",
      "user_copy_c.cc",
    ]
    deps = [
      "//zircon/kernel/dev/iommu/dummy",
      "//zircon/kernel/lib/arch",
      "//zircon/kernel/lib/cmdline",
      "//zircon/kernel/lib/console",
      "//zircon/kernel/lib/counters",
      "//zircon/kernel/lib/crashlog",
      "//zircon/kernel/lib/init",
      "//zircon/kernel/lib/ktl",
      "//zircon/kernel/lib/perfmon",
      "//zircon/kernel/lib/syscalls",
      "//zircon/kernel/object",
      "//zircon/kernel/vm",
      "//zircon/system/ulib/bitmap",
      "//zircon/system/ulib/pretty",
    ]
  }
}
